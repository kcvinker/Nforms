
-- Calendar module - Created on 15-Mar-2023 06:57 PM

require "nforms.api_messages"
require "nforms.apifuncs"
-- require "nforms.forms"
require 'nforms.controls'
require "nforms.common"
require "nforms.datetime"

--Constants
	local MCS_WEEKNUMBERS <comptime> = 0x0004
	local MCS_NOTODAYCIRCLE <comptime> = 0x0008
	local MCS_NOTODAY <comptime> = 0x0010
	local MCS_NOTRAILINGDATES <comptime> = 0x0040
	local MCS_SHORTDAYSOFWEEK <comptime> = 0x0080

	local MCM_FIRST <comptime> = 0x1000
	local MCN_FIRST <comptime> = (0-746)
	local MCM_GETCURSEL <comptime> = MCM_FIRST + 1
	local MCM_GETMINREQRECT <comptime> = MCM_FIRST + 9
	local MCM_SETCURRENTVIEW <comptime> = MCM_FIRST + 32

	local MCN_SELECT <comptime> = MCN_FIRST
	local MCN_SELCHANGE <comptime> = MCN_FIRST - 3
	local MCN_VIEWCHANGE <comptime> = MCN_FIRST - 4

	global NMSELCHANGE : type <cimport,nodecl> = @record {
        nmhdr: NMHDR,
        stSelStart: SYSTEMTIME,
        stSelEnd: SYSTEMTIME
    }
	global LPNMSELCHANGE = @*NMSELCHANGE

	global NMVIEWCHANGE : type <cimport,nodecl> = @record {
        nmhdr: NMHDR,
        dwOldView: DWORD,
        dwNewView: DWORD
    }
	global LPNMVIEWCHANGE = @*NMVIEWCHANGE

--End


local calNumber = 1
local calClass = toUTF16("SysMonthCal32")
local function calWndProc(hw: HWND, msg: UINT, wpm: WPARAM, lpm: LPARAM, scID: UINT_PTR, refData: DWORD_PTR): LRESULT <forwarddecl> end




function Calendar:createHandle() <forwarddecl> end
global CalendarParams = @record 
{
	parent: *Form, xpos: integer, ypos: integer
}

-- Public function
function Calendar.new(params: CalendarParams) : *Calendar
	local this : *Calendar = default_allocator:new(@Calendar)
	this.name = string.format("Calendar_%d", calNumber)
	this.className = calClass
	this.width = 100
	this.height = 100
	this.xpos = params.xpos == 0 and 20 or params.xpos
	this.ypos = params.ypos == 0 and 20 or params.ypos
	this.font = params.parent.font
	this.parent = params.parent
	this._style = WS_CHILD | WS_VISIBLE
	this._exStyle = 0
	this._isTextable = false
	this.backColor = params.parent.backColor
	this.foreColor = colorBlack
	this.cid = params.parent._cidNumber
    calNumber = calNumber + 1
	params.parent._cidNumber = params.parent._cidNumber + 1
	if not app.isDateInit then
		app.isDateInit = true
		app.iccEx.dwICC = ICC_DATE_CLASSES
		InitCommonControlsEx(&app.iccEx)
	end
	if params.parent.createChilds then this:createHandle() end
	return this
end

function Calendar:addContextMenu(items: span(string))
	self.contextMenu = ContextMenu.new(self)
	self.contextMenu:addItems(items)
end

--Private function
function Calendar:setCalenderStyles()
	if self.showWeekNum then self._style = self._style | MCS_WEEKNUMBERS end
    if self.noTodayCircle then self._style = self._style | MCS_NOTODAYCIRCLE end
    if self.noToday then self._style = self._style | MCS_NOTODAY end
    if self.noTrailingDates then self._style = self._style | MCS_NOTRAILINGDATES end
    if self.shortDayNames then self._style = self._style | MCS_SHORTDAYSOFWEEK end
end

--Private function
function Calendar:setSizeAndValue()
    local rct : RECT
	SendMessageW(self.handle, MCM_GETMINREQRECT, 0, (@LPARAM)(&rct))
	SetWindowPos(self.handle, nilptr, self.xpos, self.ypos, rct.right, rct.bottom, SWP_NOMOVE)
	if self.viewMode ~= ViewMode.month then
		SendMessageW(self.handle, MCM_SETCURRENTVIEW, 0, (@integer)(self.viewMode))
	end
	local st : SYSTEMTIME
	SendMessageW(self.handle, MCM_GETCURSEL, 0, (@LPARAM)(&st))
	self.value = DateTime.fromSystemTime(st)
end

-- Public function
function Calendar:createHandle()
	self:setCalenderStyles()
	createControl(self)
	if self._isCreated then
		setSubClass(self.handle, (pointer)(self), calWndProc)
		self:setSizeAndValue()
	end
end

--Private function
function Calendar:destroy()
	-- print("Calendar destroyed")
	-- DeleteObject(self._bkBrush)
end

function calWndProc(hw: HWND, msg: UINT, wpm: WPARAM, lpm: LPARAM, scID: UINT_PTR, refData: DWORD_PTR): LRESULT
	local this = (@*Calendar)((pointer)(refData))
	switch msg do
	case WM_DESTROY then
	    RemoveWindowSubclass(hw, calWndProc, scID)
		this:destroy()

	case WM_LBUTTONDOWN then leftMouseDownHandler(this, msg, wpm, lpm)
	case WM_LBUTTONUP then leftMouseUpHandler(this, msg, wpm, lpm)
	case MM_MOUSE_LB_CLICK then leftMouseClickHandler(this)
	case WM_RBUTTONDOWN then rightMouseDownHandler(this, msg, wpm, lpm)
	case WM_RBUTTONUP then rightMouseUpHandler(this, msg, wpm, lpm)
	case MM_MOUSE_RB_CLICK then rightMouseClickHandler(this)
	case WM_MOUSEHWHEEL then mouseScrollHandler(this, msg, wpm, lpm)
	case WM_MOUSEMOVE then mouseMoveHandler(this, msg, wpm, lpm)
	case WM_MOUSELEAVE then mouseLeaveHandler(this)

	case WM_CONTEXTMENU then
		if this.contextMenu ~= nilptr then this.contextMenu:show(lpm) end
		
	case MM_NOTIFY_REFLECT then
		local nm = (@LPNMHDR)(lpm)
		switch nm.code do
			case MCN_SELECT then
				local nms = (@LPNMSELCHANGE)(lpm)
				this.value = DateTime.fromSystemTime(nms.stSelStart)
				if this.onValueChanged ~= nilptr then
					local ea: EventArgs = {}
					this.onValueChanged(this, &ea)
				end
			case MCN_SELCHANGE then
				local nms = (@LPNMSELCHANGE)(lpm)
				this.value = DateTime.fromSystemTime(nms.stSelStart)
				if this.onSelectionChanged ~= nilptr then
					local ea: EventArgs = {}
					this.onSelectionChanged(this, &ea)
				end
			case MCN_VIEWCHANGE then
				local nmv = (@LPNMVIEWCHANGE)(lpm)
				this.viewMode = (@ViewMode)(nmv.dwNewView)
				this.oldView = (@ViewMode)(nmv.dwOldView)
				if this.onViewChanged ~= nilptr then
					local ea: EventArgs = {}
					this.onViewChanged(this, &ea)
				end
		end
	end
	return DefSubclassProc(hw, msg, wpm, lpm)
end
